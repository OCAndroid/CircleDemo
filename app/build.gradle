apply plugin: 'com.android.application'


// Version can be set on CI using -Dversion-build=2 when calling gradle
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = Integer.parseInt(System.getProperty('version-build', '0'))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "org.ocandroid.circledemo"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionBuild}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile new File("hockeyapp.keystore")

            if (project.hasProperty("CI") && project.property("CI")) {
                storePassword project.property("storePassword")
                keyAlias project.property("keyAlias")
                keyPassword project.property("keyPassword")
            } else {
                def propFile = new File("keystore.properties")

                if (propFile.canRead()) {
                    def props = new Properties()
                    props.load(new FileInputStream(propFile))

                    storePassword props.getProperty("storePassword")
                    keyAlias props.getProperty("keyAlias")
                    keyPassword props.getProperty("keyPassword")
                } else {
                    println 'properties file not found'
                    android.buildTypes.release.signingConfig = null
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
